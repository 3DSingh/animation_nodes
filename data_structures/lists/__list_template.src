cimport cython
from types import GeneratorType
from libc.stdlib cimport malloc, free, realloc
from libc.string cimport memcpy, memmove, memcmp
from . utils cimport predictSliceLength, makeStepPositive, removeValuesInSlice, getValuesInSlice

@cython.freelist(10)
cdef class LISTNAME:

    # Initialization and Memory Management
    ###############################################

    def __cinit__(self, Py_ssize_t length = 0, Py_ssize_t capacity = -1):
        '''
        Initialize a new object with the given length.
        You can also directly allocate more memory from the beginning
        to allow faster appending/extending without memory reallocation.
        '''
        if length < 0:
            raise ValueError("Length has to be >= 0")
        if capacity < length:
            capacity = length
        self.data = <TYPE*>malloc(sizeof(TYPE) * capacity)
        if self.data == NULL:
            raise MemoryError()

        self.length = length
        self.capacity = capacity

    def __dealloc__(self):
        if self.data != NULL:
            free(self.data)

    cdef grow(self, size_t minCapacity):
        if minCapacity < self.capacity:
            return

        cdef size_t newCapacity = minCapacity * 2 + 1
        self.data = <TYPE*>realloc(self.data, sizeof(TYPE) * newCapacity)
        if self.data == NULL:
            self.length = 0
            self.capacity = 0
            raise MemoryError()
        self.capacity = newCapacity

    cdef void shrinkToLength(self):
        cdef size_t newCapacity = max(1, self.length)
        self.data = <TYPE*>realloc(self.data, sizeof(TYPE) * newCapacity)
        self.capacity = newCapacity

    cdef replaceArray(self, TYPE* newData, size_t newLength, size_t newCapacity):
        free(self.data)
        self.data = newData
        self.length = newLength
        self.capacity = newCapacity


    # Special Methods for Python
    ###############################################

    def __len__(self):
        return self.length

    def __getitem__(self, key):
        if isinstance(key, int):
            return self.getValueAtIndex(key)
        elif isinstance(key, slice):
            return self.getValuesInSlice(key)
        else:
            raise TypeError("Expected int or slice object")

    def __setitem__(self, key, value):
        if isinstance(key, int):
            self.setValueAtIndex(key, value)
        elif isinstance(key, slice):
            self.setValuesInSlice(key, value)
        else:
            raise TypeError("Expected int or slice object")

    def __delitem__(self, key):
        if isinstance(key, int):
            self.removeValueAtIndex(key)
        elif isinstance(key, slice):
            self.removeValuesInSlice(key)
        else:
            raise TypeError("Expected int or slice object")

    def __add__(a, b):
        cdef LISTNAME newList
        try:
            newList = LISTNAME(capacity = len(a) + len(b))
            newList.extend(a)
            newList.extend(b)
        except:
            raise NotImplementedError()
        return newList

    def __mul__(LISTNAME self, Py_ssize_t amount):
        return self.repeated(amount = amount)

    def __iadd__(LISTNAME self, other):
        try:
            self.extend(other)
        except:
            raise NotImplementedError()
        return self

    def __iter__(self):
        return LISTNAMEIterator(self)

    def __contains__(self, TYPE value):
        cdef size_t i
        for i in range(self.length):
            if self.data[i] == value:
                return True
        return False

    def __richcmp__(x, y, int operation):
        if operation == 2: # '=='
            if type(x) == type(y):
                return (<LISTNAME>x).equals_SameType(y)
            if len(x) == len(y):
                return all(a == b for a, b in zip(x, y))
            return False
        raise NotImplemented()

    cdef equals_SameType(self, LISTNAME other):
        if self.length != other.length:
            return False
        cdef size_t i
        for i in range(self.length):
            if not EQUAL(self.data[i], other.data[i]): return False
        return True


    # Base operations for lists - mimic python list
    ###############################################

    cdef tryConversion(self, value, TYPE* target):
        if TRY_CONVERT_TO_C(value, &_value) != 0:
            raise TypeError("Value cannot be element of LISTNAME")

    cpdef copy(self):
        newList = LISTNAME(self.length)
        newList.overwrite(self)
        return newList

    cpdef clear(self):
        self.length = 0
        self.shrinkToLength()

    cpdef fill(self, value):
        cdef size_t i
        cdef TYPE _value
        self.tryConversion(value, &_value)
        for i in range(self.length):
            self.data[i] = _value

    cpdef append(self, value):
        if self.length >= self.capacity:
            self.grow(self.length + 1)
        self.tryConversion(value, self.data + self.length)
        self.length += 1

    cpdef extend(self, values):
        cdef size_t oldLength, newLength, i
        if isinstance(values, LISTNAME):
            self.overwrite(values, self.length)
        elif isinstance(values, list):
            self.extendList(values)
        elif isinstance(values, tuple):
            self.extendTuple(values)
        elif hasattr(values, "__len__"):
            newLength = self.length + len(values)
            self.grow(newLength)
            for i, value in enumerate(values, start = self.length):
                self.tryConversion(value, self.data + i)
            self.length = newLength
        else:
            try:
                oldLength = self.length
                for value in values:
                    self.append(value)
            except:
                self.length = oldLength
                raise TypeError("invalid input")

    cdef extendList(self, list values):
        cdef size_t newLength, i
        newLength = self.length + len(values)
        self.grow(newLength)
        for i in range(len(values)):
            self.tryConversion(values[i], self.data + self.length + i)
        self.length = newLength

    cdef extendTuple(self, tuple values):
        cdef size_t newLength, i
        newLength = self.length + len(values)
        self.grow(newLength)
        for i in range(len(values)):
            self.tryConversion(values[i], self.data + self.length + i)
        self.length = newLength

    cpdef index(self, value):
        cdef TYPE _value
        self.tryConversion(value, &_value)
        cdef Py_ssize_t index = self.searchIndex(_value)
        if index >= 0: return index
        raise ValueError("value not in list")

    cdef size_t searchIndex(self, TYPE value):
        cdef size_t i
        for i in range(self.length):
            if EQUAL(self.data[i], value):
                return i
        return -1

    cpdef size_t count(self, value):
        cdef TYPE _value
        self.tryConversion(value, &_value)
        cdef size_t i
        cdef size_t amount = 0
        for i in range(self.length):
            if EQUAL(self.data[i], _value):
                amount += 1
        return amount

    cpdef remove(self, TYPE value):
        cdef size_t index = self.searchIndex(value)
        if index == -1:
            raise ValueError("value not in list")
        else:
            self.removeValueAtIndex(index)

    cpdef insert(self, size_t index, value):
        cdef TYPE _value
        if index >= self.length:
            self.append(value)
        else:
            self.tryConversion(value, &_value)
            self.grow(self.length + 1)
            if index < 0: index += self.length
            if index < 0: index = 0
            memmove(self.data + index + 1,
                    self.data + index,
                    sizeof(TYPE) * (self.length - index))
            self.data[index] = value
            self.length += 1



    # Get/Set/Remove single element
    ################################################

    cdef getValueAtIndex(self, size_t index):
        index = self.tryCorrectIndex(index)
        return self.data[index]

    cdef setValueAtIndex(self, size_t index, TYPE value):
        index = self.tryCorrectIndex(index)
        self.data[index] = value

    cdef removeValueAtIndex(self, size_t index):
        index = self.tryCorrectIndex(index)
        memmove(self.data + index,
                self.data + index + 1,
                (self.length - index) * sizeof(TYPE))
        self.length -= 1


    # Get/Set/Remove elements in slice
    ################################################

    cdef LISTNAME getValuesInSlice(self, slice sliceObject):
        cdef:
            void* newArray
            size_t newLength
            LISTNAME newList

        getValuesInSlice(self.data, self.length, sizeof(TYPE),
                         &newArray, &newLength, sliceObject)

        newList = LISTNAME()
        newList.replaceArray(<TYPE*>newArray, newLength, newLength)
        return newList

    cdef setValuesInSlice(self, slice sliceObject, values):
        cdef Py_ssize_t start, stop, step
        start, stop, step = sliceObject.indices(len(self))

        if step == 1:
            self.setValuesInSimpleSlice(start, stop, values)
        else:
            self.setValuesInExtendedSlice(start, stop, step, values)

    cdef setValuesInSimpleSlice(self, size_t start, size_t stop, values):
        cdef:
            size_t replacementLength = len(values)
            size_t sliceLength = predictSliceLength(start, stop, 1)

        if replacementLength > sliceLength:
            self.grow(self.length + (replacementLength - sliceLength))
        if replacementLength != sliceLength:
            memmove(self.data + start + replacementLength,
                    self.data + stop,
                    sizeof(TYPE) * (self.length - stop))
            self.length += replacementLength - sliceLength

        cdef TYPE _value
        if isinstance(values, LISTNAME):
            self.overwrite(values, start)
        else:
            for i in range(replacementLength):
                self.tryConversion(values[i], self.data + start + i)

    cdef setValuesInExtendedSlice(self, Py_ssize_t start, Py_ssize_t stop, Py_ssize_t step, values):
        cdef size_t sliceLength = predictSliceLength(start, stop, step)
        if sliceLength != len(values):
            raise ValueError("attempt to assign sequence of size {} to extended slice of size {}"
                             .format(len(values), sliceLength))

        # TODO: Speedup for specific list types + use while loop
        # range does not efficiently work with a variable step
        for i, value in zip(range(start, stop, step), values):
            self.tryConversion(value, self.data + i)

    cdef removeValuesInSlice(self, slice sliceObject):
        cdef Py_ssize_t start, stop, step
        start, stop, step = sliceObject.indices(len(self))
        cdef size_t removeAmount = removeValuesInSlice(
                      arrayStart = <char*>self.data,
                      arrayLength = self.length * sizeof(TYPE),
                      elementSize = sizeof(TYPE),
                      start = start, stop = stop, step = step)
        self.length -= removeAmount


    # Create new lists based on an existing list
    ###############################################

    cpdef LISTNAME reversed(self):
        cdef:
            LISTNAME newList = LISTNAME(self.length)
            Py_ssize_t i, offset
        offset = self.length - 1
        for i in range(self.length):
            newList.data[i] = self.data[offset - i]
        return newList

    def repeated(self, *, Py_ssize_t length = -1, Py_ssize_t amount = -1):
        if length < 0 and amount < 0:
            raise ValueError("Specify the 'length' or 'amount' parameter as keyword")
        elif length > 0 and amount > 0:
            raise ValueError("Can only evaluate one parameter of 'length' and 'amount'")

        if amount >= 0:
            length = self.length * amount

        if self.length == 0 and length > 0:
            raise ValueError("Cannot repeat a list with zero elements to something longer")

        cdef:
            LISTNAME newList = LISTNAME(length)
            Py_ssize_t copyAmount = length // self.length
            Py_ssize_t i

        # Full copy as often as possible
        for i in range(copyAmount):
            memcpy(newList.data + i * self.length,
                   self.data,
                   sizeof(TYPE) * self.length)
        # Partial copy at the end
        memcpy(newList.data + self.length * copyAmount,
               self.data,
               sizeof(TYPE) * (length % self.length))
        return newList


    # Low level utilities
    ###############################################

    cdef tryCorrectIndex(self, Py_ssize_t index):
        if index < 0:
            index += self.length
        if index < 0 or index >= self.length:
            raise IndexError("list index out of range")
        return index

    cdef overwrite(self, LISTNAME other, Py_ssize_t index = 0):
        if self.capacity <= index + other.length:
            self.grow(index + other.length)
        memcpy(self.data + index, other.data, other.length * sizeof(TYPE))
        self.length = max(self.length, index + other.length)

    cdef overwriteArray(self, TYPE* array, size_t arrayLength, Py_ssize_t index):
        if self.capacity <= index + arrayLength:
            self.grow(index + arrayLength)
        memcpy(self.data + index, array, arrayLength * sizeof(TYPE))
        self.length = max(self.length, index + arrayLength)


    # Memory Views
    ###############################################

    cpdef MEMVIEW_TYPE[:] getMemoryView(self):
        if MEMVIEW_TYPE is None:
            raise NotImplementedError("Cannot create memoryview for this type")
        if self.length > 0:
            return <MEMVIEW_TYPE[:self.length]>self.data
        else:
            # hack to make zero-length memview possible
            return (<MEMVIEW_TYPE[:1]>self.data)[1:]


    # Classmethods for List Creation
    ###############################################

    @classmethod
    def join(cls, *sourceLists):
        cdef size_t newLength = 0
        cdef size_t offset = 0
        cdef LISTNAME source

        for source in sourceLists:
            newLength += len(source)
        newList = LISTNAME(newLength)
        for source in sourceLists:
            newList.overwrite(source, offset)
            offset += source.length

        return newList

    @classmethod
    def fromValues(cls, values):
        if isinstance(values, (list, tuple)):
            return cls.fromListOrTuple(values)

        try:
            from . convert import toLISTNAME
            return toLISTNAME(values)
        except TypeError: pass

        cdef LISTNAME newList = LISTNAME()
        newList.extend(values)
        return newList

    @classmethod
    def fromListOrTuple(cls, list_or_tuple values):
        newList = LISTNAME(len(values))
        cdef size_t i
        for i, value in enumerate(values):
            self.tryConversion(value, newList.data + i)
        return newList


    # String Representations
    ###############################################

    def __repr__(self):
        if self.length < 20:
            return "<LISTNAME [{}]>".format(", ".join(self[i] for i in range(min(self.length))))
        else:
            return "<LISTNAME [{}, ...]>".format(", ".join(self[i] for i in range(20)))

    def status(self):
        return "Length: {}, Capacity: {}, Size: {} bytes".format(
            self.length, self.capacity, self.capacity * sizeof(TYPE))


cdef class LISTNAMEIterator:
    '''
    Implements the 'Iterator Protocol' that is used to allow iteration
    over a custom list object (eg with a for loop).
    An instance of this class is only created in the __iter__ method
    of the corresponding list type.
    https://docs.python.org/3.5/library/stdtypes.html#iterator-types
    '''
    cdef:
        LISTNAME source
        size_t current

    def __cinit__(self, LISTNAME source):
        self.source = source
        self.current = 0

    def __iter__(self):
        return self

    def __next__(self):
        if self.current >= self.source.length:
            raise StopIteration()
        cdef TYPE currentValue = self.source.data[self.current]
        self.current += 1
        return currentValue
